#!/usr/bin/perl -w
use strict;

use IPC::Open2;
use Term::ANSIColor;
use POSIX qw(mkfifo);

# configuration
my $startdir = '/home/mitch/media';

my $mplayer_regexp        = qr/\.(mp3|avi|mkv|ogg)$/;
my $mplayer_regexp_stream = qr/\.(webradio)$/;
my $mplayer_fifo = '/tmp/omxplayer-console.mplayer.fifo';
my @mplayer_exec = ('mplayer', '-slave', '-quiet', '-input', "file=${mplayer_fifo}", '%FILES%');
my $mplayer_pid;
# see mplayer slave mode documentation at
# http://www.mplayerhq.hu/DOCS/tech/slave.txt


# global variables
my $win;
my $dir;
my @filelist;
my $sel;
my @dirstack;
my $ypos;

# cache clear screen sequence
my $cls = `clear`;

# do this once, console terminals don't resize often
my $screen_w = 0 + `tput cols`;
my $screen_h = 0 + `tput lines`;

				  
sub mplayer_stop()
{
    return unless defined $mplayer_pid;

    mplayer_send('stop');
    kill 15, $mplayer_pid;
    waitpid( $mplayer_pid, 0 );
    $mplayer_pid = undef;
}

sub mplayer_send($)
{
    open  FIFO, '>', $mplayer_fifo or die "can't open fifo `$mplayer_fifo': $!";
    print FIFO "$_[0]\n";
    close FIFO or die "can't close fifo `$mplayer_fifo': $!";

}

sub stop_all()
{
    mplayer_stop();
}

sub mplayer_start(@)
{
    stop_all();

    $mplayer_pid = fork();
    die 'fork() unsuccessful in mplayer_start()' unless defined $mplayer_pid;

    if ($mplayer_pid == 0) {
	# child

#	close STDOUT; # mplayer plays no streams when stdout is closed?!
	open(STDOUT, '>', '/dev/null') or die "can't reopen stdout to `/dev/null': $!";
	close STDIN;
	close STDERR;

	my @exec = ();
	foreach my $part (@mplayer_exec) {
	    if ($part eq '%FILES%') {
		push @exec, @_;
	    } else {
		push @exec, $part;
	    }
	}

	exec(@exec);
	# gone, we are mplayer now
	die 'exec() did not happen in mplayer_start()';
	
    }
}

sub mplayer_start_stream($)
{
    my $playlist = shift;

    open PLAYLIST, '<', $playlist or die "can't open `$playlist': $!";
    my $line = <PLAYLIST>;
    close PLAYLIST or die "can't close `$playlist': $!";

    chomp $line;

    # this is taken from jamirdochegal:
    if ($line =~ /^(?:\s*(\S+)\|)?(.*)\s+(\S+)\s*$/) {
        my ($shortcut, $name, $url, $flags) = ($1, $2, $3, {});

        while ($url =~ s/^([A-Z]+)://g) {
            $flags->{$1}++;
        }

	my @parms = ( '-cache', '128' );
	push @parms, ('-demuxer', 'ogg') if exists $flags->{OGGDEMUX};
	push @parms, '-playlist' unless exists $flags->{DIRECT};

	mplayer_start( @parms, $url );
    }

}

sub print_big($)
{
    my $text = shift;

    my ($stdin, $stdout);
    my $pid = open2(
	$stdout,
	$stdin,
	'figlet -t -f mini -k'
#	'figlet -t -f small -W'
#	'figlet -t -f small'
	);

    my $ret;

    print $stdin "$text\n";
    close $stdin;

    while (my $line = <$stdout>) {
	$ypos++;
	print $line;
    }
    close $stdout;

    waitpid( $pid, 0 );

    return $ret;
}

sub clear_screen()
{
    print $cls;
    $ypos = 1; # don't use last line on screen
}

sub show_dir()
{
    print color 'white';
    print_big($dir);
    print color 'reset';
}    

sub show_file()
{
    if ($sel >= 0) {
	my $file = $filelist[$sel];
	if ($file->{TYPE} eq 'd') {
	    print color 'bold white';
	} else {
	    print color 'bold blue';
	}
	print_big($file->{NAME});
	print color 'reset';
    } else {
	print_big('');
    }
}    

sub show_filelist()
{

    my $start = 0;
    my $end = @filelist;

    my $lines_left = $screen_h - $ypos - 2;
    if ($lines_left < @filelist) {
	$start = $sel - int($lines_left/2);
	if ($start < 0) {
	    $start = 0;
	}
	$end = $start + $lines_left;
	if ($end > @filelist) {
	    $end = @filelist;
	    $start = $end - $lines_left;
	}
    }

    if ($start > 0) {
	print colored('       -^-^-^-^-^-^-^-^-', 'bold yellow') . "\n";
    } else {
	print "\n";
    }
	    
    my $pos = 0;
    foreach my $file (@filelist) {

	unless ($pos < $start) {

	    my $filefg = $file->{TYPE} eq 'f' ? 'bold blue' : 'bold white';
	    my $filebg = $pos == $sel ? 'on_red' : '';
	
	    printf("   %s %s\n",
		   $pos == $sel ? colored('==>', 'bold yellow') : '   ',
		   colored($file->{DISPLAY}, "$filefg $filebg")
		);
	}	

	$pos++;

	last if $pos == $end;
    }

    if ($end < @filelist) {
	print colored('       -v-v-v-v-v-v-v-v-', 'bold yellow') . "\n";
    } else {
	print "\n";
    }
}

sub read_directory($)
{
    my $dir = shift;
    my @files;

    opendir DIR, $dir or die "can't opendir `$dir': $!";
    while (my $file = readdir(DIR)) {
	next if $file =~ /^\./;
	my $fullname = $dir.'/'.$file;
	if (-f $fullname) {
	    push @files, {
		TYPE => 'f',
		NAME => $file,
		FULLNAME => $fullname,
		DIR => $dir,
		DISPLAY => " $file "
	    }
	} elsif (-d _) {
	    push @files, {
		TYPE => 'd',
		NAME => $file,
		FULLNAME => $fullname,
		DIR => $dir,
		DISPLAY => "[$file]"
	    }
	}
    }
    closedir DIR or die "can't closedir `$dir': $!";

    @files = sort { $a->{TYPE} cmp $b->{TYPE}
		    ||
			$a->{NAME} cmp $b->{NAME} } @files;

    return @files;
}

sub change_directory($)
{
    my $newdir = shift;

    if (substr($newdir, 0, length ($startdir)) eq $startdir) {
	$dir = $newdir;
	@filelist = read_directory($newdir);
	$sel = @filelist ? 0 : -1;
    }
}

sub draw_screen()
{
    clear_screen();
    show_dir();
    show_file();
    show_filelist();
}

# initialize other stuff
(-p $mplayer_fifo) or mkfifo($mplayer_fifo, 0700) or die "can't mkfifo `$mplayer_fifo': $!";
change_directory($startdir);


while (1) {
    draw_screen();

    my $cmd = <>;
    chomp $cmd;
    if ($cmd eq 'up') {
	if ($sel > 0) {
	    $sel--
	}
    }
    elsif ($cmd eq 'down') {
	if ($sel < @filelist-1) {
	    $sel++
	}
    }
    elsif ($cmd eq 'quit') {
	stop_all();
	last;
    }
    elsif ($cmd eq 'left') {
	if (length $dir > length $startdir) {
	    my $newdir = $dir;
	    $newdir =~ s,/[^/]*$,,;
	    change_directory($newdir);
	    if (@dirstack) {
		$sel = pop @dirstack;
		if ($sel >= @filelist) {
		    $sel = @filelist - 1;
		}
	    }
	}
    }
    elsif ($cmd eq 'right') {
	if ($sel >= 0) {
	    my $file = $filelist[$sel];
	    if ($file->{TYPE} eq 'd') {
		push @dirstack, $sel;
		change_directory($file->{FULLNAME});
	    }
	}
    }
    elsif ($cmd eq 'power') {
	`shutdown -h now`;
    }
    elsif ($cmd eq 'play') {
	if ($sel >= 0) {
	    my $file = $filelist[$sel];
	    if ($file->{TYPE} eq 'f') {
		if ($file->{NAME} =~ /$mplayer_regexp/) {
		    mplayer_start($file->{FULLNAME});
		}
		elsif ($file->{NAME} =~ /$mplayer_regexp_stream/) {
		    mplayer_start_stream($file->{FULLNAME});
		}
	    }
	}
	
    }
    elsif ($cmd eq 'stop') {
	mplayer_stop();
    }

}

# exit
print color 'reset';

